.INCLUDE "M32DEF.INC"

; LCD NAMING INIT
.EQU LCD_DPRT = PORTD
.EQU LCD_DDDR = DDRD
.EQU LCD_CPRT = PORTB
.EQU LCD_CDDR = DDRB
.EQU LCD_RS = 1
.EQU LCD_RW = 2
.EQU LCD_EN = 3

; STACK INIT
LDI R16,HIGH(RAMEND)
OUT SPH,R16
LDI R16,LOW(RAMEND)
OUT SPL,R16

; PORT INIT
LDI R16,0xFF
OUT LCD_DDDR, R16
OUT LCD_CDDR, R16
LDI R16,0x00
OUT DDRA, R16
SBI DDRB, 0


CBI LCD_CPRT, LCD_EN
CALL DELAY_2ms

; LCD INIT
LDI R16,0x38
CALL CMNDWRT
CALL DELAY_2ms
LDI R16,0x0E
CALL CMNDWRT
LDI R16,0x01
CALL CMNDWRT
CALL DELAY_2ms
LDI R16,0x06
CALL CMNDWRT

; ADC INIT
LDI R16, 0x83 
OUT ADCSRA, R16

; 0x40 = AVCC Reference
; ADC0 Right Adjust
LDI R16, 0x40 
OUT ADMUX, R16

MAIN:
    ; Clear Display
    LDI R16,0x01
    CALL CMNDWRT
    CALL DELAY_2ms

    ; Start Conversion
    SBI ADCSRA, ADSC
WAIT_ADC:
    SBIS ADCSRA, ADIF
    RJMP WAIT_ADC
    SBI ADCSRA, ADIF

    ; Read ADC
    IN R0, ADCL
    IN R1, ADCH

    ; Temp Calculation (ADC / 2)
    LSR R1
    ROR R0
    ; R0 now has Temperature

    ; Convert to ASCII
    MOV R16, R0
    LDI R18, 10
    LDI R17, 0 ; Tens Count

GET_TENS:
    CP R16, R18
    BRCS DONE_TENS
    SUB R16, R18
    INC R17
    RJMP GET_TENS

DONE_TENS:
    ; Display Tens
    MOV R19, R16 ; Save Ones
    MOV R16, R17
    SUBI R16, -48 ; Convert to ASCII
    CALL DATAWRT
    
    ; Display Ones
    MOV R16, R19
    SUBI R16, -48 ; Convert to ASCII
    CALL DATAWRT
    
    ; Display ' C'
    LDI R16, ' '
    CALL DATAWRT
    LDI R16, 'C'
    CALL DATAWRT

	CALL CHECK_TEMP_LIMIT

	; CALL DELAY_5m
	CALL DELAY_1s
	CALL DELAY_1s

RJMP MAIN


CMNDWRT:
    OUT LCD_DPRT, R16
    CBI LCD_CPRT, LCD_RS
    CBI LCD_CPRT, LCD_RW
    SBI LCD_CPRT, LCD_EN
    CALL SDELAY
    CBI LCD_CPRT, LCD_EN
    CALL DELAY_100_us
RET

DATAWRT:
    OUT LCD_DPRT, R16
    SBI LCD_CPRT, LCD_RS
    CBI LCD_CPRT, LCD_RW
    SBI LCD_CPRT, LCD_EN
    CALL SDELAY
    CBI LCD_CPRT, LCD_EN
    CALL DELAY_100_us
RET


SDELAY:
	NOP
	NOP
RET

DELAY_100_us:
	PUSH R17
	LDI R17,60
DR0:
	CALL SDELAY
	DEC R17
BRNE DR0
	POP R17
RET

DELAY_2ms:
	PUSH R17
	LDI R17,20
LDR0:
	CALL DELAY_100_us
	DEC R17
BRNE LDR0
	POP R17
RET

DELAY_4ms:
	PUSH R17
	LDI R17,2
LDR1:
	CALL DELAY_2ms
	DEC R17
BRNE LDR1
	POP R17
RET

DELAY_1s:
	PUSH R17
	LDI R17,250
LDR2:
	CALL DELAY_4ms
	DEC R17
BRNE LDR2
	POP R17
RET

DELAY_2m:
    PUSH R20
    PUSH R21
    LDI R20, 2
LOOP_MINS:
    LDI R21, 60
LOOP_SECS:
    CALL DELAY_1s
    DEC R21
    BRNE LOOP_SECS
    DEC R20
    BRNE LOOP_MINS
    POP R21
    POP R20
RET


CHECK_TEMP_LIMIT:
    PUSH R20
    MOV R20, R0
    CPI R20, 15
    BRSH TURN_ON
    CBI PORTB, 0
    RJMP END_CHECK
TURN_ON:
    SBI PORTB, 0
END_CHECK:
    POP R20
   RET
